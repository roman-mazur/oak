apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = '1.0'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.ow2.asm:asm:5.0.3'
  compile 'org.ow2.asm:asm-tree:5.0.3'
  compile 'com.beust:jcommander:1.47'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'com.squareup.okio:okio:1.3.0'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.guava:guava:18.0'
  testCompile 'com.google.truth:truth:0.25'
  testCompile 'com.google.testing.compile:compile-testing:0.6'
}

def mainClassName = "com.rmazur.oak.Cmd"

task noDepsJar(type: Jar, dependsOn: jar) {
  archiveName = 'oak-cli.jar'
  def deps = files(configurations.runtime, jar.archivePath)
  def depClasses = deps.collect { it.isDirectory() ? it : zipTree(it) }
  from(depClasses) {
    exclude 'META-INF/MANIFEST.MF'
  }
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

afterEvaluate {
  uploadArchives {
    repositories {
      mavenDeployer {
        pom.project {
          name 'Oak'
        }
      }
    }
  }
}
